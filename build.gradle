buildscript {
    ext {
        KOTLIN_VERSION = "1.2.70"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${KOTLIN_VERSION}"
    }
}

apply plugin: "kotlin"
apply plugin: 'application'

mainClassName = "org.ethereum.lists.methodsignatures.CheckAndStatsKt"

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

task importFrom4ByteDirectory(type: JavaExec) {
    main = "org.ethereum.lists.methodsignatures.ImportOnline4ByteDirectoryKt"
    classpath = sourceSets.main.runtimeClasspath
}

task coverage(type: JavaExec) {
    main = "org.ethereum.lists.methodsignatures.CoverageKt"
    classpath = sourceSets.main.runtimeClasspath
}

task importFromSolidityDirectory(type: JavaExec) {
    main = "org.ethereum.lists.methodsignatures.ImportSolidityDirectoryKt"
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty("importPath")) {
        args importPath
    }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${KOTLIN_VERSION}"
    compile "com.github.walleth.kethereum:keccak_shortcut:0.53"
    compile "com.github.walleth.kethereum:method_signatures:0.53"
    compile 'com.beust:klaxon:0.30'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
}

